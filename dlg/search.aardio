import win.ui;
import web.form;
import web.json;
import string;
import inet;
import inet.url;
import inet.http;
import preg;
/*DSG{{*/
var winform = win.form(text="æœç´¢";right=1040;bottom=607;bgcolor=2302755)
winform.add(
BiliLogo={cls="picturebox";left=376;top=168;right=682;bottom=266;dl=1;dr=1;dt=1;image=$"\res\search_bg.jpg";z=1};
ReturnBack={cls="button";text="è¿”å›";left=968;top=552;right=1016;bottom=600;hide=1;z=4};
SearchSubmit={cls="button";text="æœç´¢";left=672;top=280;right=784;bottom=320;flat=1;font=LOGFONT(h=-16);z=3};
SearchWd={cls="edit";left=272;top=280;right=664;bottom=320;bgcolor=16777215;edge=1;font=LOGFONT(h=-20);hidesel=1;z=2}
)
/*}}*/

web.form.emulation(11001);
var wb = web.form(winform);
wb.html = `<style>body{background:#232323;margin:0;padding:0;}</style>`;

function GoBack(){
	winform.SearchSubmit.hide = false;
	winform.SearchWd.hide = false;
	winform.BiliLogo.hide = false;
	winform.ReturnBack.hide = true;
	winform.SearchWd.text ="";
	
	wb.html = `<style>body{background:#232323;margin:0;padding:0;}</style>`;
}

winform.SearchSubmit.oncommand = function(id,event){
	//è·å–æœç´¢è¯
	var SearchWord = winform.SearchWd.text;
	if (string.replace(SearchWord, " ", "") == "") {return;}
	
	//éšè—æœç´¢æ¡†
	winform.SearchSubmit.hide = true;
	winform.SearchWd.hide = true;
	winform.BiliLogo.hide = true;
	winform.ReturnBack.hide = false;
	winform.SearchWd.text ="";
	
	//æ˜¾ç¤ºæç¤ºè¯
	wb.html = "<meta charset='utf-8'><style>body{background:#232323;margin:0;padding:0;color:#FFF;}</style>æ­£åœ¨æœç´¢â€œ" + SearchWord + "â€ä¸­...";
	
	
	//è½½å…¥ç™»å½•ä¿¡æ¯
	var LoginData = "";
	try{ LoginData = string.load("./auth.data"); }catch(e){}
	if(LoginData == "" || LoginData == null) {LoginData = inet.getCookie("https://www.bilibili.com/");}
	
	
	//è¯·æ±‚æœç´¢API
	http = inet.http();
	var FeedStr = http.get(
		"https://api.bilibili.com/x/web-interface/search/all/v2?keyword=" + inet.url.encode(SearchWord),
		{ "Cookie":LoginData; }
	);
	
	//åœ¨å¿…è¦æ—¶è½¬æ¢
	if (type(FeedStr) == "string"){ FeedStr = web.json.parse(FeedStr); }
	
	//åˆ¤æ–­è¿”å›ä¿¡æ¯æ˜¯å¦é”™è¯¯
	if (FeedStr.code != 0){
		win.msgboxErr("APIè¿”å›äº†é”™è¯¯ä»£ç ï¼Œæ‚¨å¯èƒ½æœªç™»å½•", "è¯·æ±‚å¤±è´¥");
		GoBack();
		return;
	}
	
	//è·å–è§†é¢‘åˆ—è¡¨
	var VideoList;
	for (a, b in FeedStr.data.result){ if(b.result_type == "video"){ VideoList = b; } }
	
	
	var WebList = `
	<!DOCTYPE html>
	<html>
		<head>
			<meta charset="utf-8"><meta name="referrer" content="never">
			<style>
				img{width:100%;border-radius:10px;-webkit-user-drag:none;}
				a{text-decoration:none; color:#FFF;}
				body{margin:5px;background-color:#18181c;color:#FFF;font-size:15px;user-select:none;-webkit-appearance:none;font-family: Arial, "microsoft yahei";}
				.dynamic_singlebox{height:210px;width:190px;float:left;background:#18181c;overflow:hidden;border-radius:12px;margin:8px;padding:5px;transition:all 0.3s;cursor:pointer;}
				.dynamic_singlebox:hover{transform:scale(1.1,1.1);transform-origin:center center;background:#3b3b3b;}
				.dynamic_singlebox_un{color:#848484;font-size:10px;position:relative;top:8px;width:100%;overflow:hidden;}
				.dynamic_singlebox_vt{color:#FFFFFF;width:100%;height:60px;overflow:hidden;}
				.keyword{ color:#00b8f6; }
			</style>
		</head>
		<body>
	`;
	for(i,itm in VideoList.data){
		try{
			WebList += `
				<div class='dynamic_singlebox'>
					<a href="#` + itm.bvid + `">
						<img src='http:` + itm.pic + `@240w_135h_1c.jpg'><br>
						<div class="dynamic_singlebox_vt">` + itm.title + `</div>
					</a>
					<a href="#UID` + itm.mid + `"><div class="dynamic_singlebox_un">ğŸ”˜&nbsp;` + itm.author + `</div></a>
				</div>`;
		}catch(e){}
	}
	
	wb.html = WebList + "</body></html>";
}

//æ­¤å¤„åˆ¤æ–­é“¾æ¥æ˜¯å¦æ”¹å˜
var origper = "default";
winform.setInterval(
	function(){
		var per = string.split(wb.location + '#default','#')[2];
		if (per != origper) {
			if(string.indexAny(per, "BV") != null){
				win.loadForm("\dlg\player.aardio");
				publish("bvid", per);
			} else {
				win.loadForm("\dlg\space.aardio");
				
				publish("uid", string.replace(per, "UID", "") );
			}
			origper = per;
		}
	},1000
);

winform.ReturnBack.oncommand = function(id,event){
	GoBack();
}

winform.show();
win.loopMessage();
return winform;