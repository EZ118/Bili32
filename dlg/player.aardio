import win.ui;
import win.ui.menu;
import win.clip;

import web.json;
import web.view;

import inet.url;
import inet.http;
import string.list;
/*DSG{{*/
var winform = win.form(text="Video Player";right=759;bottom=469;bgcolor=1973790)
winform.add(
BvidStore={cls="edit";left=704;top=440;right=752;bottom=464;edge=1;hide=1;z=1};
MyMsg={cls="static";text="分享链接已保存到剪贴板！";left=248;top=208;right=472;bottom=240;align="center";bgcolor=15793151;center=1;font=LOGFONT(h=-16);hide=1;z=2}
)
/*}}*/

var wb = web.view(winform);
var StartCnt = 0;

subscribe("bvid", function(bvid){
	if (StartCnt == 0) { StartCnt = 1; } else { return; }
	
	winform.BvidStore.text = bvid;
	
    //wb.userAgent = "Mozilla/5.0 (Windows NT 10.0; WOW64; rv:56.0) Gecko/20100101 Firefox/56.0";
    //wb.go("https://www.bilibili.com/blackboard/webplayer/activity-embed-other-old.html?bvid=" + bvid + "&page=1&danmaku=0");
    wb.go("https://www.bilibili.com/blackboard/html5mobileplayer.html?bvid=" + bvid + "&page=1&danmaku=0");
    
    blocker = `
    	/*
        function RemoveAds() {
            try{ document.getElementsByClassName("bilibili-player-video-sendjumpbar")[0].remove(); }catch(e){}
            try{ document.getElementsByClassName("bilibili-player-video-pause-panel-container-qrcode")[0].remove(); }catch(e){}
            try{ document.getElementsByClassName("bilibili-player-video-suspension-bar-btn-group-jumpbtn")[0].remove(); }catch(e){}
        }
        setTimeout(function(){ RemoveAds(); }, 4000);
        setTimeout(function(){ RemoveAds(); }, 7000);*/
        
		var current = true;
        
        document.addEventListener('keydown',(e)=>{
      		if(e.ctrlKey&&e.keyCode===87){
        		location.href="about:blank";
      		}
      		if(e.keyCode===32) {
      		    var video = document.getElementsByTagName("video")[0];
				if(current){
					video.play();
					current = false; //下次点击的状态
				}else{
					video.pause();
					current = true;
				}
      		}
    	});
        `;
   wb.doScript(blocker);
   
});

winform.setInterval( function(){ if (wb.location == "about:blank") { winform.close(); } }, 2000 );


var menu = win.ui.menu(winform);
menu.add('视频详情和评论',function(id){
	bvid = winform.BvidStore.text;
	http = inet.http();
	
	var VideoInfo = web.json.parse(http.get("https://api.bilibili.com/x/web-interface/view?bvid=" + bvid));
    var aid = VideoInfo.data.aid;
    var desc = VideoInfo.data.desc;
    
    var ReplyInfo = web.json.parse(http.get("https://api.bilibili.com/x/v2/reply?jsonp=jsonp&pn=1&type=1&sort=2&oid=" + aid));
    var UserShow = "";
    for(i,j in ReplyInfo.data.replies) {
		try {
			UserShow += "【" + j.member.uname + "】 " + j.content.message + '\n';
		} catch(e){}
	}
    
    var DetailForm = win.loadForm("\dlg\video_detail.aardio");
    DetailForm.VideoDetail.text = desc;
    DetailForm.VideoReview.text = UserShow;
});

menu.add('点赞',function(id){
	bvid = winform.BvidStore.text;
	
	if (!io.exist("./auth.data")) { win.msgbox("点暂失败", "未登录"); return; }
	var LoginData = string.load("./auth.data");					//读取auth.data
	
	var cookielist = string.list(LoginData, ";", "=" );			//将auth.data中的字符串转为table
	var jct = inet.url.decode(cookielist["bili_jct"], true);	//从登录信息中获取用户凭据
	
	http = inet.http();
	http.addHeaders = "Cookie: " + LoginData;
	var FeedStr = web.json.parse(http.post(
		"https://api.bilibili.com/x/web-interface/archive/like",
		"bvid=" + bvid + "&like=1&csrf=" + jct,
		{ "Cookie":LoginData; }
	));
	
	if (FeedStr.code == 0) { win.msgbox("点赞成功", "返回[0]"); }
	else if (FeedStr.code == 65006) { win.msgbox("已赞过", "返回[65006]"); }
	else { win.msgbox("点赞失败 (" + FeedStr.message + ")", "返回[" + FeedStr.code + "]"); }
});

menu.add('分享',function(id){
	//win.clip.write("https://www.bilibili.com/video/" + winform.BvidStore.text + "/?from=bili32_share");
	win.clip.write("https://b23.tv/" + winform.BvidStore.text);
	
	winform.MyMsg.hide = false;
	winform.setTimeout(function(){ winform.MyMsg.hide = true; }, 3000);
});

/* ctrl+w 关闭窗口 */
import win.ui.accelerator
win.ui.accelerator({
	{ ctrl = true; vkey = 'W'#; oncommand = function(){winform.close();} } 
},winform);

winform.show();
win.loopMessage();
return winform;